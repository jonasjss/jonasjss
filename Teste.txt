HTML_SKU_Table = 
VAR _Source = 'Sheet1'[Source Name]
VAR _Impact = 'Sheet1'[SKUs Impacted]
VAR _list   = SUBSTITUTE ( _Impact, ",", "|" )
VAR _n      = PATHLENGTH ( _list )
VAR _rows =
    ADDCOLUMNS (
        GENERATESERIES ( 1, _n ),
        "SKU", TRIM ( PATHITEM ( _list, [Value], TEXT ) )
    )

-- divide em grupos de 3
VAR _groups =
    ADDCOLUMNS (
        _rows,
        "RowGroup", INT ( ( [Value] - 1 ) / 5 ) + 1
    )

VAR _skusTable =
    "<table style='width:100%;border-collapse:collapse;font-size:11px'>" &
    CONCATENATEX (
        SUMMARIZE ( _groups, [RowGroup] ),
        "<tr>" &
            CONCATENATEX (
                FILTER ( _groups, [RowGroup] = EARLIER ( [RowGroup] ) ),
                "<td style='padding:4px;border:1px solid #ddd;text-align:center'>" & [SKU] & "</td>",
                ""
            ) &
        "</tr>",
        ""
    ) &
    "</table>"

VAR _open =
    "<table style='width:100%;border-collapse:collapse;font-family:Segoe UI;font-size:12px'>"
VAR _head =
    "<thead><tr style='background:#1A3D51;color:#fff'>
        <th style='padding:6px;border:1px solid #ddd'>Source Name</th>
        <th style='padding:6px;border:1px solid #ddd'>SKUs</th>
    </tr></thead>"
VAR _body =
    "<tbody><tr>
        <td style='padding:6px;border:1px solid #ddd'>" & _Source & "</td>
        <td style='padding:0;border:1px solid #ddd'>" & _skusTable & "</td>
    </tr></tbody>"
RETURN
IF (
    ISBLANK ( _Impact ),
    BLANK (),
    _open & _head & _body & "</table>"
)
